plugins {
    id 'com.github.johnrengelman.shadow'
    id 'maven-publish'
    id "net.minecrell.plugin-yml.bukkit" version "0.4.0"
}

bukkit {
    name = "GKCore"
    version = project.version
    main = "me.GK.core.GKCore"
    description = "A core library for all the plugins on the server GKPixel"
    softDepend = ["MySQL"]
    authors = ["G7", "RobotHanzo"]
    prefix = "GKCore"
    commands {
        gk {
            description = 'GKPixel commands'
            usage = '/gk <subcommand> [arguments]'
        }
        gkcallback {
            description = 'GKPixel callbacks for internal usage'
            usage = 'You are not supposed to use this on your own'
        }
    }
}

configurations {
    provided
    implementation.extendsFrom(provided)
}

repositories {
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url = "https://jitpack.io" }
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url "https://repo.dmulloy2.net/repository/public/" }

    flatDir {
        dirs 'libs'
    }
}

compileJava.options.encoding = 'UTF-8'

dependencies {
    implementation 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    implementation 'org.objenesis:objenesis:3.2'
    implementation 'me.clip:placeholderapi:2.10.9'
    implementation group: "com.comphenix.protocol", name: "ProtocolLib", version: "4.7.0"
    implementation 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    //CloudNet
    implementation 'com.github.CloudNetService:CloudNet-v3:3.4.0'
    provided name: 'cloudnet-bridge'
}

task sourceJar(type: Jar) {
    archiveFileName = "GKCore-source.jar"
    from sourceSets.main.allJava
    from configurations.provided.asFileTree.files.collect { zipTree(it) }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackageRegistry"
            url = "https://maven.pkg.github.com/GKPixel/GKCore"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
            publications {
                gpr(MavenPublication) {
                    from components.java
                    artifact sourceJar {
                        classifier "sources"
                    }
                    pom.with {
                        groupId 'me.gk'
                        artifactId 'gkcore-spigot'
                        version project.version
                    }
                    pom.withXml {
                        asNode().dependencies.dependency.each { dep ->
                            if(dep.artifactId.last().value().last() in ["cloudnet-bridge"]) {
                                assert dep.parent().remove(dep)
                            }
                        }
                    }
                }
            }
        }
    }
}

tasks.build.dependsOn tasks.shadowJar

//common
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

jar {
    archiveFileName = 'GKCore.jar'
}

task copyDocs(type: Copy) {
    if (System.getenv("GKPLUGIN_DIR") != null) {
        from "build/libs/GKCore.jar"
        into "${System.getenv("GKPLUGIN_DIR")}/v${project.GKCoreVersion}/spigot"
    }
}
tasks.build.finalizedBy(copyDocs)
