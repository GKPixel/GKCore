plugins {
    id 'com.github.johnrengelman.shadow'
    id 'maven-publish'
    id "net.minecrell.plugin-yml.bungee" version "0.4.0"
}

bungee {
    name = "GKCore"
    version = project.version
    main = "me.GK.core.GKCore"
    description = "A core library for all the plugins on the server GKPixel"
    author = "RobotHanzo"
}

repositories {
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    mavenCentral()
}

dependencies {
    compileOnly 'net.md-5:bungeecord-api:1.16-R0.4'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'version': project.version
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    repositories {
        maven {
            name = "GitHubPackageRegistry"
            url = "https://maven.pkg.github.com/GKPixel/GKCore"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
            publications {
                gpr(MavenPublication) {
                    from(components.java)
                    artifact sourceJar {
                        classifier "sources"
                    }
                    pom.with {
                        groupId 'me.gk'
                        artifactId 'gkcore-bungee'
                        version project.version
                    }
                }
            }
        }
    }
}

jar {
    archiveFileName = 'GKCore.jar'
}

tasks.build.dependsOn tasks.shadowJar
